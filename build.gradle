apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'application'

group = "io.github.carlomicieli.java8"
version = "1.2.0-SNAPSHOT"
description = "Random experimentation with Java 8"

mainClassName = "io.github.carlomicieli.java8.MainClass"

compileJava {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

[compileJava, compileTestJava].options*.compilerArgs = [
        "-Xlint:serial", "-Xlint:varargs", "-Xlint:cast", "-Xlint:classfile",
        "-Xlint:dep-ann", "-Xlint:divzero", "-Xlint:empty", "-Xlint:finally",
        "-Xlint:overrides", "-Xlint:path", "-Xlint:processing", "-Xlint:static",
        "-Xlint:try", "-Xlint:fallthrough", "-Xlint:rawtypes", "-Xlint:deprecation",
        "-Xlint:unchecked", "-Xlint:-options", "-Werror"
]

repositories {
    mavenCentral()
}

def logbackVersion = "1.0.13"
def slf4jVersion = "1.7.2"
def log4jVersion = "1.2.17"
dependencies {
    compile "org.slf4j:slf4j-api:${slf4jVersion}"
    runtime "org.slf4j:jcl-over-slf4j:${slf4jVersion}",
            "log4j:log4j:${log4jVersion}",
            "ch.qos.logback:logback-classic:${logbackVersion}",
            "ch.qos.logback:logback-core:${logbackVersion}"

    compile "com.google.code.gson:gson:2.3"
    compile "org.apache.commons:commons-lang3:3.2.1"
    testCompile "junit:junit:4.11"
    testCompile "org.assertj:assertj-core:1.6.1"
    testCompile "org.hamcrest:hamcrest-core:1.3",
                "org.hamcrest:hamcrest-library:1.3"
}

task wrapper(type: Wrapper, 
             group: 'Gradle wrapper', 
             description: 'Gradle will be automatically downloaded and used to run the build.') {
    gradleVersion = '2.1'
}

test {
    testLogging {
        exceptionFormat = 'full'
        events 'skipped', 'failed'
    }
}
